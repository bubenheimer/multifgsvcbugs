pluginManagement {
    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }
}

dependencyResolutionManagement {
    repositories {
        google()
        mavenCentral()
    }

    versionCatalogs {
        libs {
            version('android-plugin', '8.0.2')

            version('android-sdk-compile', 'android-34')
            version('android-sdk-min', '26')
            version('android-sdk-target', '34')

            version('androidx-activity', '1.7.2')
            version('androidx-compose-bom', '2023.05.01')
            version('androidx-compose-compiler', '1.4.7')

            version('androidx-core', '1.10.1')

            version('java', JavaVersion.VERSION_11.toString())

            version('kotlin', '1.8.21')


            plugin('android-application', 'com.android.application')
                    .versionRef('android-plugin')

            plugin('kotlin-android', 'org.jetbrains.kotlin.android')
                    .versionRef('kotlin')


            library('androidx-activity-ktx', 'androidx.activity', 'activity-ktx')
                    .versionRef('androidx-activity')

            library('androidx-compose-activity', 'androidx.activity', 'activity-compose')
                    .versionRef('androidx-activity')
            library('androidx-compose-bom', 'androidx.compose', 'compose-bom')
                    .versionRef('androidx-compose-bom')
            library('androidx-compose-foundation', 'androidx.compose.foundation', 'foundation')
                    .withoutVersion()
            library('androidx-compose-material', 'androidx.compose.material3', 'material3')
                    .withoutVersion()
            library('androidx-compose-runtime', 'androidx.compose.runtime', 'runtime')
                    .withoutVersion()
            library('androidx-compose-ui', 'androidx.compose.ui', 'ui')
                    .withoutVersion()

            library('androidx-core-ktx', 'androidx.core', 'core-ktx')
                    .versionRef('androidx-core')
        }
    }
}

include(":app")
